#!/usr/bin/env perl
package password;

use strict;
use warnings;

our $VERSION = '3.04';

__PACKAGE__->_run( \@ARGV ) unless caller;

sub new {
    my $class = shift;
    my $self  = {
        length  => undef,
        special => undef,
        @_,
    };

    die "length must be a positive integer\n"
        if $self->{length} !~ /^\d+$/ || $self->{length} < 1;

    return bless $self, $class;
}

sub generate {
    my $self = shift;

    require Math::Random::Secure;

    my @characters = ( 'A' .. 'Z', 'a' .. 'z', 0 .. 9 );

    if ( $self->{special} ) {
        push @characters,
             ( '!', '$', '%', '&', '*',
               '+', '-', '.', '#', ':',
               '=', '?', '@', '^', '_' );
    }

    return join '', map {
        $characters[ Math::Random::Secure::irand( scalar @characters - 1 ) ]
    } 1 .. $self->{length}
}

sub _run {
    my $class = shift;
    my $args  = shift;

    require Getopt::Long;
    require Pod::Usage;

    my %opt = (
        length  => 12,
        special => 0,
    );

    Getopt::Long::GetOptionsFromArray(
        $args,
        \%opt,
        'length=i',
        'special!',
        'version',
        'help',
    ) or Pod::Usage::pod2usage( -exitval => 1, -verbose => 0 );

    Pod::Usage::pod2usage( -exitval => 0, -verbose => 1 ) if $opt{help};

    if ( $opt{version} ) {
        print "$VERSION\n";
        return;
    }

    delete %opt{version};
    delete %opt{help};

    my $object = $class->new(%opt);

    print $object->generate() . "\n";

    return;
}

1;

__END__

=pod

=head1 NAME

password - generate cryptographically secure passwords

=head1 SYNOPSIS

 password [--length <int>] [--special|--no-special]
          [--version] [--help]

=head1 OPTIONS

=over

=item --length <int>

how long you want the password (defaults to 12)

=item --special | --no-special

should there be special characters in the password

=item --version

print the version and exit

=item --help

print this dialogue

=back

=head1 DESCRIPTION

C<password> generates cryptographically secure random passwords using alphanumeric characters with case variation and optional special characters.

C<password> can be used as a script or as a perl module.

=head1 INSTALLATION

To install C<password> as a script.

=over

=item Generate the build and install tooling

 perl Makefile.PL

=item Build, test, and install

 make
 make test && make install

=back

=head1 EXAMPLES

=head2 As a script

=over

=item Get a 12 character length password

 password

=item Get a 32 character length password

 password --length 32

=item Get a 32 character length password with special characters

 password --length 32 --special

=back

=head2 As a module

=over

=item Get a 12 character length password

 require 'password';
 my $object   = password->new( length => 12 );
 my $password = $object->generate();

=item Get a 32 character length password

 require 'password';
 my $object   = password->new( length => 32 );
 my $password = $object->generate();

=item Get a 32 character length password with special characters

 require 'password';
 my $object   = password->new( length => 32, special => 1 );
 my $password = $object->generate();

=back

=head1 CONSTRUCTOR

=head2 new

Constructor for the password object.

=head3 ARGUMENTS

=over

=item length

The length of the password to be generated.  Must be a positive integer.

=back

=head3 RETURNS

The password object.

=head3 EXCEPTIONS

=over

=item length must be a positive integer

This exception is thrown when verification of the length argument doesn't pass.

=back

=head1 METHODS

=head2 generate

Generates the password based on the length as defined through the constructor.

=head3 ARGUMENTS

None.

=head3 RETURNS

The generated password string.

=head1 EXIT STATUS

The following exit statuses are returned when run as a script.

=over

=item 0 - Indicates completion without failure

=item 1 - Indicates failure

=back

=head1 DEPENDENCIES

=over

=item L<Getopt::Long>

=item L<Pod::Usage>

=item L<Math::Random::Secure>

=back

=head1 AUTHOR

Blaine Motsinger, <blaine@renderorange.com>

=head1 LICENSE AND COPYRIGHT

This software is available under the MIT license.

Copyright (c) 2022 Blaine Motsinger

=cut
