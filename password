#!/usr/bin/env perl

use strictures;

use Getopt::Long  ();
use Pod::Usage    ();
use Crypt::Random ();

my $VERSION = '1.0.2';

use constant {
    DEFAULT_LENGTH => 12,
    EXIT_VALUES    => {
        SUCCESS => 0,
        FAILURE => 1,
    },
};

my $opt = {};
Getopt::Long::GetOptions(
    'length=i' => \$opt->{length},
    'version'  => \$opt->{version},
    'help'     => \$opt->{help},
    'man'      => \$opt->{man},
) or Pod::Usage::pod2usage( EXIT_VALUES->{FAILURE} );

Pod::Usage::pod2usage( -exitval => EXIT_VALUES->{SUCCESS}, -verbose => 2, noperldoc => 1 ) if $opt->{man};
Pod::Usage::pod2usage(EXIT_VALUES->{SUCCESS}) if $opt->{help};

if ( $opt->{version} ) {
    print "$VERSION\n";
    exit EXIT_VALUES->{SUCCESS};
}

$opt->{length} ||= DEFAULT_LENGTH;    # default to 12

my @characters = ( 'A' .. 'Z', 'a' .. 'z', 0 .. 9 );
my $password   = join '', map {
    $characters[ Crypt::Random::makerandom_itv( Lower => 0, Upper => scalar @characters ) ]
} 1 .. $opt->{length};

print "$password\n";
exit EXIT_VALUES->{SUCCESS};

__END__

=pod

=head1 NAME

password - generate cryptographically secure passwords

=head1 SYNOPSIS

  password [--length] <int>
           [--version]
           [--help] [--man]

=head1 OPTIONS

  --length     how long you want the password (defaults to 12)
  --version    print the version and exit
  --help       print this dialogue
  --man        display the full documentation

=head1 DESCRIPTION

This script generates cryptographically secure random passwords, using alphanumeric characters with case variation.

=cut

=head1 EXAMPLES

=over

=item Get a 12 character length password

  password

=item Get a 32 character length password

  password --length 32

=back

=head1 EXIT STATUS

=over

=item 0 - Indicates completion without failure

=item 1 - Indicates failure

=back

=head1 DEPENDENCIES

=over

=item strictures

=item Getopt::Long

=item Pod::Usage

=item Crypt::Random

=back

=head1 AUTHOR

Blaine Motsinger, <blaine@renderorange.com>

=head1 LICENSE AND COPYRIGHT

This software is available under the MIT license.

Copyright (c) 2018 Blaine Motsinger

=cut
